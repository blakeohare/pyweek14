// persistent state cannot save strings with newlines across "forever" sessions

function get_persistent_state() {
	if (PersistentState.INSTANCE == null) {
		PersistentState.INSTANCE = new PersistentState();
	}
	return PersistentState.INSTANCE;
}

function load_persistent_state() {
	get_persistent_state().load_game();
}

function save_persistent_state() {
	get_persistent_state().save_game();
}

function get_persisted_forever_int(name) { return get_persistent_state().get_int_forever(name); }
function get_persisted_session_int(name) { return get_persistent_state().get_int_session(name); }
function get_persisted_level_int(name) { return get_persistent_state().get_int_level(name); }

function get_persisted_forever_string(name) { return get_persistent_state().get_string_forever(name); }
function get_persisted_session_string(name) { return get_persistent_state().get_string_session(name); }
function get_persisted_level_string(name) { return get_persistent_state().get_string_level(name); }

function set_persisted_forever_int(name, value) { return get_persistent_state().set_int_forever(name, value); }
function set_persisted_session_int(name, value) { return get_persistent_state().set_int_session(name, value); }
function set_persisted_level_int(name, value) { return get_persistent_state().set_int_level(name, value); }

function set_persisted_forever_string(name, value) { return get_persistent_state().set_string_forever(name, value); }
function set_persisted_session_string(name, value) { return get_persistent_state().set_string_session(name, value); }
function set_persisted_level_string(name, value) { return get_persistent_state().set_string_level(name, value); }

function increment_persisted_forever_int(name, amount) {
	set_persisted_forever_int(name, get_persisted_forever_int(name) + amount);
}

function increment_persisted_level_int(name, amount) {
	set_persisted_level_int(name, get_persisted_level_int(name) + amount);
}

function increment_persisted_session_int(name, amount) {
	set_persisted_session_int(name, get_persisted_session_int(name) + amount);
}

function persistence_change_level() {
	get_persistent_state().level = {};
}

class PersistentState {
	constructor() {
		self.forever = {};
		self.session = {};
		self.level = {};
	}
	
	function purge_all() {
		self.forever = {};
		self.session = {};
		self.level = {};
	}
	
	function save_game() {
		UserData.fileWriteText('save.txt', self.serialize());
	}
	
	function change_level() {
		self.level = {};
	}
	
	function serialize() {
		output = [];
		for (key : self.forever.keys()) {
			value = self.forever[key];
			if (value[1] == None) {
				value[1] = '';
			}
			output.add(value[0] + key + ':' + str(value[1]));
		}
		return output.join('\n');
	}
	
	function load_game() {
		t = None;
		if (UserData.fileExists('save.txt')) {
			t = UserData.fileReadText('save.txt');
		}
		values = {};
		if (t != None) {
			lines = t.split('\n');
			for (line : lines) {
				parts = line.split(':');
				if (len(parts) >= 2) {
					key = parts[0];
					value = parts[1:].join(':');
					if (len(key) >= 2) {
						type = key[0];
						name = key[1:];
						if (type == 'i') {
							value = Core.parseInt(value) ?? 0;
						} else {
							type = 's';
						}
						values[name] = [type, value];
					}
				}
			}
		}
		
		self.forever = values;
	}
	
	function set_int_forever(name, value) {
		self._set_int(name, value, self.forever);
	}
	
	function set_int_session(name, value) {
		self._set_int(name, value, self.session);
	}
	
	function set_int_level(name, value) {
		self._set_int(name, value, self.level);
	}
	
	function get_int_forever(name) {
		return self._get_int(name, self.forever);
	}
	
	function get_int_session(name) {
		return self._get_int(name, self.session);
	}
	
	function get_int_level(name) {
		return self._get_int(name, self.level);
	}
	
	function set_string_forever(name, value) {
		self._set_string(name, value, self.forever);
	}
	
	function set_string_session(name, value) {
		self._set_string(name, value, self.session);
	}
	
	function set_string_level(name, value) {
		self._set_string(name, value, self.level);
	}
	
	function get_string_forever(name) {
		return self._get_string(name, self.forever);
	}
	
	function get_string_session(name) {
		return self._get_string(name, self.session);
	}
	
	function get_string_level(name) {
		return self._get_string(name, self.level);
	}
	
	function _set_int(name, value, lookup) {
		lookup[name] = ['i', value];
	}
	
	function _set_string(name, value, lookup) {
		lookup[name] = ['s', value];
	}
	
	function _get_int(name, lookup) {
		value = lookup.get(name);
		if (value != None && value[0] == 'i') {
			return value[1];
		}
		return 0;
	}
	
	function _get_string(name, lookup) {
		value = lookup.get(name);
		if (value != None && value[0] == 's') {
			return value[1];
		}
		return '';
	}
}
